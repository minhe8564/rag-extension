pipeline {
    agent any

    options {
        skipDefaultCheckout(true)
    }

    parameters {
        booleanParam(name: 'BUILD_QUERY_EMBEDDING', defaultValue: true, description: 'Query Embedding Serviceë¥¼ ìˆ˜ë™ìœ¼ë¡œ ë¹Œë“œí•˜ê³  ë°°í¬í•˜ë ¤ë©´ ì²´í¬í•˜ì„¸ìš”.')
        string(name: 'BRANCH_TO_BUILD', defaultValue: 'develop', description: 'ìˆ˜ë™ ë¹Œë“œ ì‹œ ê¸°ì¤€ ë¸Œëœì¹˜ë¥¼ ì„ íƒí•˜ì„¸ìš” (develop ë˜ëŠ” main).')
        booleanParam(name: 'CLEANUP_ONLY', defaultValue: false, description: 'ì˜¤ë˜ëœ ì»¨í…Œì´ë„ˆ/ì´ë¯¸ì§€ ì •ë¦¬ë§Œ ìˆ˜í–‰')
    }

    environment {
        // Image & Container
        QUERY_EMBEDDING_IMAGE_NAME = "hebees/query-embedding"
        QUERY_EMBEDDING_CONTAINER  = "hebees-query-embedding"

        // Networks
        APP_NETWORK_TEST = "app-network-test"
        APP_NETWORK_PROD = "app-network-prod"
        DB_NETWORK = "db-network"
    }

    stages {
        stage('Checkout') {
            steps {
                checkout scm
                sh 'ls -al'
            }
        }

        stage('Update uv.lock') {
            when {
                anyOf {
                    expression { env.GITLAB_OBJECT_KIND == 'push' }
                    expression { params.BUILD_QUERY_EMBEDDING == true }
                }
            }
            steps {
                sh '''
                set -eux
                # Docker ì»¨í…Œì´ë„ˆì—ì„œ uv lock ì‹¤í–‰ (ê¶Œí•œ ë¬¸ì œ íšŒí”¼)
                docker run --rm -v "$PWD":/app -w /app python:3.11-slim bash -c "
                    apt-get update -qq && apt-get install -y -qq curl ca-certificates >/dev/null 2>&1 && \
                    curl -fsSL https://astral.sh/uv/install.sh | sh && \
                    /root/.local/bin/uv lock && \
                    chown -R $(id -u):$(id -g) uv.lock 2>/dev/null || true
                "
                echo "uv.lock updated successfully"
                '''
            }
        }

        stage('Prepare Docker Networks') {
            when {
                anyOf {
                    expression { env.GITLAB_OBJECT_KIND == 'push' }
                    expression { params.BUILD_QUERY_EMBEDDING == true }
                }
            }
            steps {
                sh "docker network create ${APP_NETWORK_TEST} || true"
                sh "docker network create ${APP_NETWORK_PROD} || true"
                sh "docker network create ${DB_NETWORK} || true"
            }
        }

        stage('Build Docker Image') {
            when {
                anyOf {
                    expression { env.GITLAB_OBJECT_KIND == 'push' }
                    expression { params.BUILD_QUERY_EMBEDDING == true }
                }
            }
            steps {
                script {
                    // ë¸Œëœì¹˜ ê²°ì •: ì›¹í›…(ref) ìš°ì„ , ì—†ìœ¼ë©´ íŒŒë¼ë¯¸í„°
                    def branch = ''
                    if (env.GITLAB_OBJECT_KIND == 'push') {
                        branch = (env.REF ?: '').replaceAll('refs/heads/', '').trim()
                    }
                    if (!branch) {
                        branch = (params.BRANCH_TO_BUILD ?: '').trim()
                    }
                    if (!branch) { error '[Build Docker Image] ë¸Œëœì¹˜ê°€ ë¹„ì–´ ìˆìŠµë‹ˆë‹¤.' }
                    echo "ë¹Œë“œ ëŒ€ìƒ ë¸Œëœì¹˜: ${branch}"

                    def tag = "${QUERY_EMBEDDING_IMAGE_NAME}:${env.BUILD_NUMBER}"
                    sh """
                    set -eux
                    docker build -t ${tag} .
                    """
                    env.QUERY_EMBEDDING_BUILD_TAG = tag
                }
            }
        }

        stage('Deploy Docker Container') {
            when {
                anyOf {
                    expression { env.GITLAB_OBJECT_KIND == 'push' }
                    expression { params.BUILD_QUERY_EMBEDDING == true }
                }
            }
            steps {
                withCredentials([file(credentialsId: 'query-embedding-repo.env', variable: 'QUERY_EMBEDDING_ENV_FILE')]) {
                    sh '''
                    set -eux
                    # ê¸°ì¡´ ì»¨í…Œì´ë„ˆ ì¢…ë£Œ/ì‚­ì œ
                    docker stop "$QUERY_EMBEDDING_CONTAINER" || true
                    docker rm "$QUERY_EMBEDDING_CONTAINER" || true

                    # ì»¨í…Œì´ë„ˆ ì‹¤í–‰: --env-fileë¡œ í™˜ê²½ ë³€ìˆ˜ ì£¼ì…
                    docker run -d \
                        --name "$QUERY_EMBEDDING_CONTAINER" \
                        --restart unless-stopped \
                        --network "$APP_NETWORK_TEST" \
                        --network "$APP_NETWORK_PROD" \
                        --network "$DB_NETWORK" \
                        --env-file "$QUERY_EMBEDDING_ENV_FILE" \
                        "$QUERY_EMBEDDING_BUILD_TAG"
                    '''
                }
            }
        }

        stage('Health Check') {
            when {
                anyOf {
                    expression { env.GITLAB_OBJECT_KIND == 'push' }
                    expression { params.BUILD_QUERY_EMBEDDING == true }
                }
            }
            steps {
                script {
                    def maxRetries = 30
                    def ok = false
                    for (int i = 0; i < maxRetries; i++) {
                        def status = sh(script: '''
                            docker run --rm --network "$APP_NETWORK_TEST" curlimages/curl:8.8.0 \
                                -fsS http://$QUERY_EMBEDDING_CONTAINER:8000/health >/dev/null
                        ''', returnStatus: true)
                        if (status == 0) { ok = true; break }
                        sleep 2
                    }
                    if (!ok) { error "Health check failed for ${QUERY_EMBEDDING_CONTAINER}" }
                }
            }
        }

        stage('Cleanup Old Images (Optional)') {
            when { expression { params.CLEANUP_ONLY == true } }
            steps {
                sh "docker image prune -f || true"
            }
        }
    }

    post {
        always {
            script {
                // ê³µí†µ ì •ë³´ ìˆ˜ì§‘ (í•œ ë²ˆë§Œ ì‹¤í–‰)
                def branch    = resolveBranch()
                def mention   = resolvePusherMention()
                def commitMsg = sh(script: "git log -1 --pretty=%s", returnStdout: true).trim()
                def commitUrl = env.GIT_COMMIT_URL ?: ""
                
                def buildInfo = [
                    branch   : branch,
                    mention  : mention,
                    buildUrl : env.BUILD_URL,
                    commit   : [msg: commitMsg, url: commitUrl]
                ]
                
                // ë¹Œë“œ ê²°ê³¼ì— ë”°ë¼ ì•Œë¦¼ ì „ì†¡
                if (currentBuild.result == 'SUCCESS' || currentBuild.result == null) {
                    echo "ğŸ‰ POST: ë¹Œë“œ ì„±ê³µ â€“ Mattermost ì•Œë¦¼ ì „ì†¡"
                    sendMMNotify(true, buildInfo)
                    
                } else if (currentBuild.result == 'FAILURE') {
                    echo "ğŸš¨ POST: ë¹Œë“œ ì‹¤íŒ¨ â€“ ë¡œê·¸ ì¶”ì¶œ í›„ Mattermost ì•Œë¦¼ ì „ì†¡"
                    
                    // Jenkins ë‚´ì¥ APIë¡œ ë¡œê·¸ ì¶”ì¶œ (ë§ˆì§€ë§‰ 150ì¤„)
                    try {
                        def rawBuild = currentBuild.rawBuild
                        def logText = rawBuild.getLog(150).join('\n')
                        
                        // ë¯¼ê°ì •ë³´ ë§ˆìŠ¤í‚¹
                        logText = logText
                            .replaceAll(/(?i)(token|secret|password|passwd|apikey|api_key)\s*[:=]\s*\S+/, '$1=[REDACTED]')
                            .replaceAll(/AKIA[0-9A-Z]{16}/, 'AKIA[REDACTED]')
                        
                        buildInfo.details = "```text\n${logText}\n```"
                    } catch (Exception e) {
                        echo "âš ï¸ ë¡œê·¸ ì¶”ì¶œ ì‹¤íŒ¨: ${e.message}"
                        buildInfo.details = "```text\në¡œê·¸ë¥¼ ê°€ì ¸ì˜¬ ìˆ˜ ì—†ìŠµë‹ˆë‹¤.\n```"
                    }
                    
                    sendMMNotify(false, buildInfo)
                }
                
                echo "ğŸ“¦ Pipeline finished with status: ${currentBuild.currentResult}"
            }
        }
    }
}

// ë¸Œëœì¹˜ í•´ì„: BRANCH_NAME â†’ GIT_REF â†’ git
def resolveBranch() {
    if (env.BRANCH_NAME) return env.BRANCH_NAME
    if (env.REF) return env.REF.replaceFirst(/^refs\/heads\//, '')
    return sh(script: "git name-rev --name-only HEAD || git rev-parse --abbrev-ref HEAD", returnStdout: true).trim()
}

// @username (ì›¹í›…ì˜ user_username) ìš°ì„ , ì—†ìœ¼ë©´ ì»¤ë°‹ ì‘ì„±ì í‘œì‹œ
def resolvePusherMention() {
    def u = env.GIT_PUSHER_USERNAME?.trim()
    if (u) return "@${u}"
    return sh(script: "git --no-pager show -s --format='%an <%ae>' HEAD", returnStdout: true).trim()
}

// ë§¤í„°ëª¨ìŠ¤íŠ¸ ì•Œë¦¼ ì „ì†¡
def sendMMNotify(boolean success, Map info) {
    def titleLine = success ? "## :jenkins7: Query Embedding ì„œë¹„ìŠ¤ ë¹Œë“œ ì„±ê³µ âœ…"
                            : "## :angry_jenkins: Query Embedding ì„œë¹„ìŠ¤ ë¹Œë“œ ì‹¤íŒ¨ âŒ"
    def lines = []
    if (info.mention) lines << "**ì‘ì„±ì**: ${info.mention}"
    if (info.branch)  lines << "**ëŒ€ìƒ ë¸Œëœì¹˜**: `${info.branch}`"
    if (info.commit?.msg) {
        def commitLine = info.commit?.url ? "[${info.commit.msg}](${info.commit.url})" : info.commit.msg
        lines << "**ì»¤ë°‹**: ${commitLine}"
    }
    if (!success && info.details) {
        lines << "**ì—ëŸ¬ ë¡œê·¸**:\n${info.details}"
    }
    
    def text = "${titleLine}\n" + (lines ? ("\n" + lines.join("\n")) : "")
    
    // ì•ˆì „ ì „ì†¡(í¬ë¦¬ë´ì…œ ê²½ê³  ì—†ìŒ)
    writeFile file: 'payload.json', text: groovy.json.JsonOutput.toJson([
        text      : text,
        username  : "Jenkins",
        icon_emoji: ":jenkins7:"
    ])
    
    withCredentials([string(credentialsId: 'mattermost-webhook', variable: 'MM_WEBHOOK')]) {
        sh(script: '''
            curl -sS -f -X POST -H 'Content-Type: application/json' \
                --data-binary @payload.json \
                "$MM_WEBHOOK" || true
        ''')
    }
}

